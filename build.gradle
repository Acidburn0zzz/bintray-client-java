import static java.lang.System.getenv

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.2.0'
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'artifactory-publish'

    group = 'com.jfrog.bintray.client'
    final env = getenv()



    def artifactoryUrl = 'https://oss.jfrog.org'
    if (artifactoryUrl) {
        artifactory {
            contextUrl = artifactoryUrl
            resolve {
                repository {
                    repoKey = 'libs-release'
                }
            }

            publish {
                repository {
                    repoKey = 'oss-snapshot-local'   //The Artifactory repository key to publish to
                    username = project.hasProperty('bintrayUser') ? project.bintrayUser : getenv()['BINTRAY_USER']
                    password = project.hasProperty('bintrayKey') ? project.bintrayKey : getenv()['BINTRAY_KEY']
                }
                defaults {
                    publishArtifacts = true
                }
            }
        }
    } else {
        repositories {
            jcenter()
        }
    }

}

subprojects() {
    apply plugin: 'java'

    sourceCompatibility = 1.5
    targetCompatibility = 1.5

    dependencies {
        compile 'joda-time:joda-time:2.3'
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }
}

project(':api') {
    publishing {
        publications {
            api(MavenPublication) {

                artifactId 'bintray-client-java-api'

                from components.java
                artifact sourceJar {
                    classifier 'sources'
                }
            }
        }
    }

    artifactoryPublish {
        publications(project(':api').publishing.publications.api)
    }
}

project(':impl') {
    apply plugin: 'groovy'

    dependencies {
        compile project(':api')
        compile 'org.codehaus.groovy:groovy-all:1.8.6'
        compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.6'
        testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
        testCompile group: 'com.timgroup', name: 'jgravatar', version: '1.0'
    }

    publishing {
        publications {
            impl(MavenPublication) {

                artifactId 'bintray-client-java-impl'

                from components.java
                artifact sourceJar {
                    classifier 'sources'
                }
            }
        }
    }

    artifactoryPublish {
        publications(project(':impl').publishing.publications.impl)
    }

    test {
        testLogging {
            exceptionFormat "full"
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            minGranularity 0
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

idea {
    project {
        jdkName = '1.7'
        languageLevel = '1.5'
        wildcards += '?*.gradle'
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                // Use git
                def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
                vcsConfig.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}
